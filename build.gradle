plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'cspforjava'
version '1.1.10'
// 1.1.0 was available from jcentre() repository
//version 1.1.8 Java8 available as a GitHub release
// version 1.1.9 Java8 available as a Github Package
// version 1.1.10 Java 11 available as Github Package

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set( "sources")
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
}

artifacts {
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/CSPforJAVA/jcsp"
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
    publications {
        jcspPUB(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}