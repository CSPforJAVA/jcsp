buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

group 'jcsp'
version '1.1.0'

apply plugin: 'java'

sourceCompatibility = 1.5

Properties properties = new Properties()
try {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
} catch (Exception e) {
    logger.warn("Missing local.properties - won't be able to upload to Bintray.  Please add local.properties with properties bintrayUser and bintrayApiKey.")
}

allprojects {
	repositories {
		jcenter()
	}
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'java'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			groupId group
			artifactId rootProject.name
			version version
            
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
	}
}

bintray {
	user = properties.containsKey('bintrayUser') ? properties.getProperty('bintrayUser') : System.getenv('BINTRAY_USER')
	key = properties.containsKey('bintrayApiKey') ? properties.getProperty('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['MyPublication']
	pkg {
		repo = 'jcsp'
		name = rootProject.name
		userOrg = user
		licenses = ['Apache-2.0', 'LGPL-2.1']
		vcsUrl = 'https://github.com/JonKerridge/jcsp.git'
		labels = ['csp', 'concurrency']
		publicDownloadNumbers = true
		version {
			name = project.property('version')
			desc = 'JCSP'
		}
	}
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}





